import groovy.json.JsonSlurper

plugins {
    id "java"
}

repositories {
    mavenCentral()
    maven { url "https://repo.gradle.org/gradle/libs-releases" }
    flatDir { dirs mcreator_path + '/lib' }
}

group = 'net.ovonsame'
version = new JsonSlurper().parse(file('src/main/resources/plugin.json'))['info']['version']

dependencies {
    implementation project(':MCreator')
    afterEvaluate {
        project(":MCreator").afterEvaluate {
            def deps = configurations.named("implementation").get().dependencies.toList()
            deps.each {
                dependencies.add("implementation", it)
            }
        }
    }
    implementation 'org.apache.logging.log4j:log4j-core:2.17.1'
    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
}

tasks.jar {
    archiveFileName.set("CAS.zip")
}

tasks.register('runMCreatorWithPlugin', JavaExec) {
    dependsOn jar

    environment("MCREATOR_PLUGINS_FOLDER", file("./build/libs"))
    environment("MCREATOR_PLUGINS_DEV", "")

    jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'

    classpath = project(':MCreator').sourceSets.main.runtimeClasspath

    main = 'net.mcreator.Launcher'

    workingDir = mcreator_path
}

subprojects { subproject ->
    subproject.plugins.withType(JavaPlugin) {
        test {
            dependsOn rootProject.jar

            environment("MCREATOR_PLUGINS_FOLDER", new File(rootProject.projectDir, "/build/libs"))
            environment("MCREATOR_PLUGINS_DEV", "")

            workingDir = mcreator_path
        }
    }
}
